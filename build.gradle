apply plugin: 'sonar-runner'

allprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'idea'


  sourceSets {
    integtest {
      java {
        compileClasspath += main.output
        runtimeClasspath += main.output
        srcDir 'src/integtest/java'
      }
      resources {
        srcDir 'src/integtest/resources'
      }
    }
  }

  idea {
    module {
      testSourceDirs += file('src/integtest/java')
      testSourceDirs += file('src/integtest/resources')
    }
  }

  task "integtest"(type: Test, dependsOn: integtestClasses) {
    testClassesDir = sourceSets.integtest.output.classesDir
    classpath = sourceSets.integtest.runtimeClasspath
  }

  configurations {
    integtestCompile.extendsFrom testCompile
  }

  tasks.withType(Test) {
    afterTest { descriptor, result ->
        logger.info("Executed test ${descriptor.getClassName().substring(descriptor.getClassName().lastIndexOf('.') + 1 )}.${descriptor.name} (${result.getEndTime() - result.getStartTime()}ms): ${result.getResultType()}")
    }

    testLogging {
        exceptionFormat "full"
    }

    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
  }
}

sonarRunner {
    sonarProperties {
        property "sonar.projectName", "test-samples"
        property "sonar.projectKey", "jsebfranck.samples"
    }
}

